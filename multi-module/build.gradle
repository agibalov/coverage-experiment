allprojects {
    apply plugin: 'jacoco'
}

subprojects {
    afterEvaluate { Project project ->
        project.tasks.findByName('jacocoTestReport').dependsOn('test')
    }
}

repositories {
    mavenCentral()
}

task jacocoFullReport(type: JacocoReport) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    executionData project.files({
        getJacocoReportTasks().executionData
    })

    classDirectories = project.files({
        getJacocoReportTasks().collect { it.classDirectories }.findAll { it != null }
    })

    sourceDirectories = project.files({
        getJacocoReportTasks().collect { it.sourceDirectories }.findAll { it != null }
    })
}

def getJacocoReportTasks() {
    return project.allprojects.collect {
        it.tasks.withType(JacocoReport).findAll { it != jacocoFullReport }
    }.flatten()
}

jacocoFullReport.dependsOn {
    subprojects*.test
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

task clean(type: Delete) {
    delete 'build'
}
